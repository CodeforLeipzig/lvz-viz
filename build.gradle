plugins {
  id 'com.adarshr.test-logger' version '3.0.0'
  id 'com.avast.gradle.docker-compose' version '0.14.9'
  id 'com.github.ben-manes.versions' version '0.39.0'
  id 'com.gorylenko.gradle-git-properties' version '2.3.1'
  id 'eclipse-wtp'
  id 'idea'
  id 'io.franzbecker.gradle-lombok' version '5.0.0'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'jacoco'
  id 'java'
  id 'org.ec4j.editorconfig' version '0.0.3'
  id 'org.sonarqube' version '3.3'
  id 'org.springframework.boot' version '2.3.12.RELEASE'
}

group = 'de.codefor.le'
version = '2.1.0-SNAPSHOT'

defaultTasks 'build'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

springBoot {
  mainClassName = 'de.codefor.le.LvzViz'
  buildInfo()
}

bootJar {
  launchScript()
}

configurations.implementation {
  exclude group: "org.hamcrest"
}

repositories {
  mavenCentral()
}
dependencies {
  implementation "com.google.guava:guava:30.1.1-jre",
                 "org.jsoup:jsoup:1.14.3"

  implementation("edu.stanford.nlp:stanford-corenlp:3.9.2") {
    // because https://app.snyk.io/vuln/SNYK-JAVA-COMGOOGLEPROTOBUF-173761
    exclude group: "com.google.protobuf", module: "protobuf-java"
    // because https://app.snyk.io/vuln/SNYK-JAVA-XALAN-31385
    exclude group: "xalan", module: "xalan"
    // because https://app.snyk.io/vuln/SNYK-JAVA-XERCES-31585
    exclude group: "xerces", module: "xercesImpl"
  }

  implementation "org.springframework.boot:spring-boot-starter-web",
                 "org.springframework.boot:spring-boot-starter-logging",
                 "org.springframework.boot:spring-boot-starter-data-elasticsearch",
                 "org.springframework.boot:spring-boot-starter-actuator"

  testImplementation("org.springframework.boot:spring-boot-starter-test") {
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
}

dockerCompose {
  captureContainersOutput = false
}

editorconfig {
  includes = ['src/**', '*']
  excludes = ['**/images', '**/vendor', '*.gz', '*.log']
}

check.dependsOn editorconfigCheck

generateGitProperties {
  // see https://github.com/n0mer/gradle-git-properties/issues/35
  onlyIf {
    !source.isEmpty()
  }
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled false
  }
}

check.dependsOn jacocoTestReport

test {
  testLogging {
    exceptionFormat = 'full'
  }
  maxHeapSize = "2048m"
  useJUnitPlatform()
}

testlogger {
  showStandardStreams false
}

// see http://www.rehpoehler.de/gradle/Gradle-Eclipse-WTP-Beispiel.html
eclipse {
  project {
    natures = ['org.springframework.ide.eclipse.core.springnature',   // if you use springsourcr sts
               'org.springsource.ide.eclipse.gradle.core.nature',     // gradle plugin
               'org.eclipse.jdt.core.javanature',                     // default java
               'org.eclipse.wst.common.modulecore.ModuleCoreNature',  // default
               'org.eclipse.wst.common.project.facet.core.nature']    // if you want a facetted projekt

    buildCommand 'org.springframework.ide.eclipse.core.springbuilder' // Spring STS
  }

  classpath {
    defaultOutputDir = file('build/classes')

    downloadSources = true
    downloadJavadoc = false
  }

  wtp {
    facet {
      facet name: 'jst.web', version: '3.1' // Servlet 3.1
    }
  }
}
