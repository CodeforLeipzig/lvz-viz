buildscript {
	repositories { mavenCentral() }
	dependencies { classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.7.RELEASE") }
}

apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'
apply plugin: 'spring-boot'

sourceCompatibility="1.8"
targetCompatibility="1.8"
group="de.codefor.le"
version="1.1.0-SNAPSHOT"

defaultTasks 'build'

springBoot { mainClass = 'de.codefor.le.LvzViz' }

repositories { mavenCentral() }
dependencies {
	def slf4jVersion = "1.7.12"

	compile "org.slf4j:slf4j-api:$slf4jVersion",
			"com.google.guava:guava:18.0",
			"org.jsoup:jsoup:1.8.1",
			"joda-time:joda-time:2.7",
			"edu.stanford.nlp:stanford-corenlp:3.5.0"

	compile "org.springframework.boot:spring-boot-starter-web",
			"org.springframework.boot:spring-boot-starter-logging",
			"org.springframework.boot:spring-boot-starter-data-elasticsearch",
			"org.springframework.boot:spring-boot-starter-actuator"

	runtime "org.slf4j:jcl-over-slf4j:$slf4jVersion",
			"ch.qos.logback:logback-classic:1.1.3"

	testCompile "org.springframework.boot:spring-boot-starter-test",
			"junit:junit:4.12"
}

// see http://www.rehpoehler.de/gradle/Gradle-Eclipse-WTP-Beispiel.html
eclipse {
    project {
        natures = ['org.springframework.ide.eclipse.core.springnature',   // if you use springsourcr sts
                   'org.springsource.ide.eclipse.gradle.core.nature',     // gradle plugin
                   'org.eclipse.jdt.core.javanature',                     // default java
                   'org.eclipse.wst.common.modulecore.ModuleCoreNature',  // default
                   'org.eclipse.wst.common.project.facet.core.nature']    // if you want a facetted projekt
        
        buildCommand 'org.springframework.ide.eclipse.core.springbuilder' // Spring STS
    }
  
    classpath {
        defaultOutputDir = file('build/classes')

        downloadSources = true
        downloadJavadoc = false
    }
  
    wtp {
        facet {
            facet name: 'jst.web', version: '3.0' // Servlet 3.0
        }
    }
}

task wrapper(type: Wrapper) { gradleVersion = "2.7" }
